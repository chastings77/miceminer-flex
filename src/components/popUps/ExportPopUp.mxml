<?xml version="1.0" encoding="utf-8"?>

	<!--
		Â© Rico Leuthold [rleuthold@access.ch] // 2009
		
		This program is free software: you can redistribute it and/or modify
	    it under the terms of the GNU General Public License as published by
	    the Free Software Foundation, either version 3 of the License, or
	    (at your option) any later version.
	
	    This program is distributed in the hope that it will be useful,
	    but WITHOUT ANY WARRANTY; without even the implied warranty of
	    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
	    GNU General Public License for more details.
	
	    You should have received a copy of the GNU General Public License
	    along with this program.  If not, see <http://www.gnu.org/licenses/>.
	-->
	
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:com="*"
	styleName="dataLoader"	
	horizontalAlign="center"
	verticalAlign="middle"
	horizontalGap="5"
	initialize="Globs.broker.addEventListener(EventsGlobals.EXPORT_COMPLETE,initiateDownload);"
	creationComplete="initComp()">
	
	<mx:Script>
		<![CDATA[
			import mx.core.IFlexDisplayObject;
			import mx.core.Application;
			import mx.managers.CursorManager;
			import mx.events.EffectEvent;
			import classes.GlobalVars;
			import mx.controls.Spacer;
			import mx.controls.LinkButton;
			import mx.controls.Alert;
			import classes.rpc.GetDataByMethod;
		
			import classes.events.*;
			import mx.managers.PopUpManager;
			import flash.net.URLRequest;
			
			/**
			 * Dispatched when the cancel button is clicked.
			 * 
			 * @eventType flash.events.Event
			 */
			[Event(name="cancelLoad", type="flash.events.Event")]
			
			[Bindable]
			[Embed(source="/assets/img/close.png")]
            private var cancelIcon:Class;
            
            [Bindable]
			[Embed(source="/assets/img/ok.png")]
            private var okIcon:Class;
			
			private var _cancelBut:LinkButton;
			private var _fileRef:FileReference;
			private var _request:URLRequest;
			private var _deleteMethod:String;
			
			
			private function initComp():void
			{
				
				PopUpManager.centerPopUp(this);
				showHide.play([this]);
				
				_deleteMethod = GlobalVars.getInstance().ConfigXML.flex.helpers.deleteFile.@method;
				
			}
			
			/**
			 * The text shown as the <code>ProgressBar</code> label.
			 */
			public function set exportLabel(text:String):void
			{
				export_bar.label = text + " ...";
				export_bar.invalidateSize();
			} 
			
			
			/**
			 * @private
			 * 
			 * initiate file download
			 */
			private function initiateDownload(event:ObjectDataEvent):void
			{
				
				_request = new URLRequest(event.values[0]);
				//_remote_filesize 
				
				Globs.broker.removeEventListener(EventsGlobals.EXPORT_COMPLETE,initiateDownload);
				event.stopImmediatePropagation();
				
				_fileRef = new FileReference();
				currentState = "fileReadyState";
				
				Application.application.addEventListener(KeyboardEvent.KEY_DOWN, handleKeyDown);
			}
			
			/**
			 * download by user mouse click
			 */
			private function downloadFileByMouseClick(event:MouseEvent):void
			{
				_fileRef.download(_request);
				registerDownloadListeners();
				currentState = "downloadFileState";
			}
			
			/**
			 * download / cancel by hitting the enter or esc key
			 */
			private function handleKeyDown(event:KeyboardEvent):void
			{
				if(event.charCode == Keyboard.ENTER) {
					_fileRef.download(_request);
					registerDownloadListeners();
					currentState = "downloadFileState";
				} else if (event.charCode == Keyboard.ESCAPE) {
					cancelBut.dispatchEvent( new MouseEvent(MouseEvent.CLICK) );
				}
				
			}
			
			/**
			 * Register file reference download listeners
			 */
			private function registerDownloadListeners():void
			{
				_fileRef.addEventListener(Event.CANCEL, downloadHandler);
				_fileRef.addEventListener(Event.COMPLETE, downloadHandler);
				_fileRef.addEventListener(Event.OPEN, downloadHandler);
				_fileRef.addEventListener(Event.SELECT, downloadHandler);
				_fileRef.addEventListener(HTTPStatusEvent.HTTP_STATUS, downloadHandler);
				_fileRef.addEventListener(IOErrorEvent.IO_ERROR, downloadHandler);
				_fileRef.addEventListener(SecurityErrorEvent.SECURITY_ERROR, downloadHandler);
			}
			
			
			private function downloadHandler(event:Event):void {
				
				switch(event.type)
				{
					// delete file from the server when the user hits the canel button or the download is complete
					case 'complete':
					case 'cancel':
						Globs.broker.addEventListener(EventsGlobals.GET_DATA_BY_METHOD_RESULT,fileRemoved);
						Globs.broker.addEventListener(EventsGlobals.GET_DATA_BY_METHOD_FAULT,fileRemovalFailed);
						var delFileMethod:GetDataByMethod = new GetDataByMethod(_deleteMethod, [_request.url]);
					break;
					case 'open':
					case 'select':
						//exportLabel = 'downloading file ...';
					break;
					case 'ioError':
					case 'securityError':
						Alert.show("ExportPopUp [downloadHandler]: An error occured => " + event.type.toString());
						_fileRef.dispatchEvent(new Event(Event.CANCEL));
					break;	
				}
				
			}
			
			/**
			 * Set to <code>true</code> to have a cancel button which dispatches a bubbeling <code>cancelLoad</code> event when clicked.
			 * 
			 * @param cancel Show cancel button or not.
			 */
			public function set cancelable(cancel:Boolean):void
			{
				if(cancel) {
					
					_cancelBut = new LinkButton();
					_cancelBut.id = 'cancelBut';
					_cancelBut.setStyle("icon", cancelIcon);
					_cancelBut.setStyle("rollOverColor", 0xFFFFFF);
					_cancelBut.setStyle("selectionColor", 0xFFFFFF);
					_cancelBut.addEventListener(MouseEvent.CLICK, cancelLoad);
					_cancelBut.toolTip = "Click to Cancel Data Export"; 
					
					addChild(_cancelBut);
					
				} else {
					if( _cancelBut != null && this.contains(_cancelBut) ) {
						this.removeChild( _cancelBut);
						
					}
				}
			}
			
			/**
			 * Cancel the export process.
			 */
			private function cancelLoad(event:MouseEvent):void
			{
				if(_fileRef) {
					_fileRef.dispatchEvent( new Event(Event.CANCEL) );
				}
				
				dispatchEvent( new Event("cancelLoad") );
				closeThis();
			}
			
			/**
			 * Close this popUp.
			 */
			public function closeThis():void
			{
				Application.application.removeEventListener(KeyboardEvent.KEY_DOWN, handleKeyDown);
				addEventListener(EffectEvent.EFFECT_END, removeThis);
				showHide.play([this],true);
			}
			
			/**
			 * Remove the popup without playing the effect.
			 */
			private function removeThis(event:EffectEvent):void
			{
				this.removeEventListener(MouseEvent.MOUSE_OVER, removeBusyCursor);
				this.removeEventListener(MouseEvent.MOUSE_OUT, setBusyCursor);
				CursorManager.removeBusyCursor();
				
				PopUpManager.removePopUp(this);	
			}
			
			private function fileRemoved(event:ObjectDataEvent):void
			{
				var result:Number = event.values[0];
				closeThis();
				
				if( result != 1) {
					Alert.show('could not remove file', 'File removal failed');
				}
				
				Globs.broker.removeEventListener(EventsGlobals.GET_DATA_BY_METHOD_FAULT,fileRemovalFailed);
            	Globs.broker.removeEventListener(EventsGlobals.GET_DATA_BY_METHOD_RESULT,fileRemoved);
            	closeThis();
            	
			}
			
			private function fileRemovalFailed(event:ObjectDataEvent):void
			{
				closeThis();
				Alert.show(event.values[0].toString(), 'File removal failed');
				Globs.broker.removeEventListener(EventsGlobals.GET_DATA_BY_METHOD_FAULT,fileRemovalFailed);
            	Globs.broker.removeEventListener(EventsGlobals.GET_DATA_BY_METHOD_RESULT,fileRemoved);
			}
			
			/**
			 * @private
			 */
			private function setBusyCursor(event:MouseEvent):void
			{
				CursorManager.setBusyCursor();
			}
			
			/**
			 * @private
			 */
			private function removeBusyCursor(event:MouseEvent):void
			{
				CursorManager.removeBusyCursor();
			}
			
			
		]]>
	</mx:Script>
	
	<!-- states -->
	<mx:states>
		
		<mx:State name="fileReadyState">
			<mx:SetProperty target="{export_bar}" name="includeInLayout" value="false" />
			<mx:SetProperty target="{export_bar}" name="visible" value="false" />
			<mx:SetProperty target="{fileReady}" name="includeInLayout" value="true" />
			<mx:SetProperty target="{fileReady}" name="visible" value="true" />
		</mx:State>
		
		<mx:State name="downloadFileState">
			<mx:SetProperty target="{export_bar}" name="includeInLayout" value="true" />
			<mx:SetProperty target="{export_bar}" name="visible" value="true" />
			<mx:SetProperty target="{export_bar}" name="label" value="downloading file ..." />
		</mx:State>
	</mx:states>
	
	<!-- effects -->
	<mx:Parallel id="showHide">
		<mx:children>
			<mx:Fade id="fadeEff" alphaFrom="0" alphaTo="1" duration="500" />
			<mx:Zoom zoomWidthFrom="0" zoomWidthTo="1" zoomHeightFrom="0" zoomHeightTo="1" duration="500" /> 		
		</mx:children>
	</mx:Parallel>
	
	<mx:ProgressBar id="export_bar" styleName="dataLoaderPB"
		labelPlacement="bottom" height="20" indeterminate="true" remove="{fadeEff.play([export_bar],true)}"/>
		
		
	<mx:VBox id="fileReady" width="100%" height="100%" includeInLayout="false" visible="false"
		paddingLeft="10"
		paddingRight="10" 
		verticalGap="1">
		<mx:Label styleName="genLabelBright" fontSize="16" fontWeight="bold" text="File ready" />
		<mx:HRule strokeColor="#FFFFFF" strokeWidth="1" width="100%"/>
		<mx:Spacer height="5" />
		<mx:Label styleName="genLabelBright" text="The file is ready to be downloaded." />
		<mx:Spacer height="10" />
		<mx:HBox width="100%">
			<mx:Button id="downloadBut" click="downloadFileByMouseClick(event)"
				styleName="normButton"  width="100%" fontSize="14" icon="{okIcon}"  
				label="Download" add="{fadeEff.play([downloadBut])}"/>
			<mx:Spacer width="100%" />	
			<mx:Button id="cancelBut" click="cancelLoad(event)"
				styleName="normButton"  width="100%" fontSize="14" icon="{cancelIcon}"  
				label="Cancel" add="{fadeEff.play([downloadBut])}"/>					
		</mx:HBox>
			
	</mx:VBox>
	
	
</mx:HBox>
